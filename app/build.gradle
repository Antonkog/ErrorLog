plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'maven-publish'
}

android {
    compileSdk versions.compileSdkVersion

    defaultConfig {
        minSdk versions.minSdk
        targetSdk targetSdkVersion
        versionCode = globalVersionCode()
        versionName = generateVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

private Integer globalVersionCode() {
    return +versions.versionMajor * 10000000 + versions.versionMinor * 10000 + versions.versionPatch
}

private String generateVersionName() {
    return "${versions.versionMajor}.${versions.versionMinor}.${versions.versionPatch}"
}

dependencies {
    //initial:
    implementation 'androidx.core:core-ktx:1.7.0'
    //json parsing
    implementation 'com.google.code.gson:gson:2.8.6'
    //okHttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.9.0"))
    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp")

}

publishing {
    Properties properties = new Properties()
    def propertiesFile = project.rootProject.file('local.properties')
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }
    def filePath = properties.getProperty('errorLogPath')

    publications {
        ErrorLog(MavenPublication) {
            groupId 'Abona'
            artifactId 'ErrorLog'
            version = generateVersionName()
            artifact filePath
        }
    }
// Repositories *to* which Gradle can publish artifacts
    repositories {
        maven {
            url 'https://pkgs.dev.azure.com/ABONA/_packaging/ABONA/maven/v1'
            credentials {
                username "ABONA"
                password System.getenv("SYSTEM_ACCESSTOKEN") != null ? System.getenv("SYSTEM_ACCESSTOKEN") : vstsMavenAccessToken
            }
        }
    }

}